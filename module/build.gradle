plugins {
    alias(libs.plugins.android.library)
    id 'maven-publish'
}

android {
    namespace 'com.inseye.shared'
    compileSdk 35

    defaultConfig {
        minSdk 27
        targetSdk 35
        versionName '0.0.3'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        versionCode 1
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        aidl true
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

    libraryVariants.configureEach { variant ->
        variant.outputs.all {
            outputFileName = "inseye_client_api_${variant.name}-${defaultConfig.versionName}.aar"
        }
    }
}

/*
 * Last edit: 3.12.2024, 10:59 by Mateusz Chojnowski mateusz.chojnowski@inseye.com
 * Copyright (c) Inseye Inc. - All rights reserved.
 *
 * All information contained herein is, and remains the property of
 * Inseye Inc. The intellectual and technical concepts contained herein are
 * proprietary to Inseye Inc. and may be covered by U.S. and Foreign Patents, patents
 * in process, and are protected by trade secret or copyright law. Dissemination
 * of this information or reproduction of this material is strictly forbidden
 * unless prior written permission is obtained from Inseye Inc. Access to the source
 * code contained herein is hereby forbidden to anyone except current Inseye Inc.
 * employees, managers or contractors who have executed Confidentiality and
 * Non-disclosure agreements explicitly covering such access.
 */

// this is for future mavenCentral or own maven publish repo
publishing {
    publications {
        create(MavenPublication) {
            groupId = 'com.inseye'
            artifactId = 'inseye-client-api'
            version = '0.0.3-alpha'

            // This is the key line
            afterEvaluate {
                from components.release
            }
        }
    }
    repositories {
        maven {
            url = uri("file://${rootProject.buildDir}/repo")
        }
    }
}

dependencies {

    implementation libs.androidx.appcompat
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
}
